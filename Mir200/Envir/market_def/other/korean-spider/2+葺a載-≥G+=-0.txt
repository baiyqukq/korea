;무기
%100
+5
+6
+43

;대화, '*' : 대화 태그
[@main]
#IF
checkpkpoint 2
#SAY
자네와 같이 불량한 사람과는 거래 하고 싶지가 않군요.. \ \
<닫  기/@exit>
#ELSEACT
goto @main-1

[@main-1]
어서오시오.  찾아줘서 고맙소.\ \
 무기를 <산다/@buy>\
 무기를 <판다/@sell>\
 무기를 <고친다/@repair>\
 대화를 <한다/@QUEST>\
 무기에 대해 <물어본다/@qweapon>\
 <그만둔다/@exit>
<>

[@QUEST]
#IF
check [755] 1
#ACT
goto @Q705
break 

#IF
check [754] 1
#ACT
goto @Q704
break

#IF
check [753] 1
#ACT
goto @Q703
#ELSEACT
goto @normal

[@Q705]
자네 빨리 도우미님에게 가보게 도우미님이 하실 말씀이 \
있다고 하네 \ \
<닫  기/@exit>

[@Q704]
#IF
check [754] 1
checklevel 5
#ACT
goto @Q704_1
#ELSEACT
goto @Q704_2

[@Q704_2]
#IF
#SAY
자네의 레벨이 아직 5가 되지 않았구만.. 천천히 능력을 향상 \
시키시게.. 나는 자네가 레벨이 5이상이 될때 까지 기다림세.. \ \
<닫  기/@exit>

[@Q704_1]
#IF
#SAY 
<$USERNAME>,자네 레벨 5까지 키웠구만.. \ 
자네의 능력이 향상이 되어가는 것을 보내 내 마음이 기쁘구만.. \
능력이 향상 되었으니, 그 능력에 맞는 검이 필요 하겠지. \
자 청동검일세, 능력을 더욱 향상 시키는데 도움이 될껄세. \ 
자 이제 도우미님에게 다시 가보시게.. \ \
<닫 기/@exit>
#ACT
SET [755] 1
give 청동검 1

[@Q703]
#IF
checklevel 3
#SAY
<$USERNAME>, 안녕하신가? 그래 도우미님에게 말을 듣고 왔다고? \
흠 자네 내가 부탁 할일은 자네의 능력을 키워 오라는 걸세.. \ 
자네의 레벨을 5까지만 키워 오시게.. \ \
부탁을 <승낙한다/@exit> \
부탁을 들어주지 <않는다/@Q703_2>
#ACT
SET [754] 1
#ELSESAY
<$USERNAME>, 안녕하신가? 그래 도우미님에게 말을 듣고 왔다고? \
하지만 자네의 능력이 너무 낮아서 부탁을 할수 없구만.. \
자네의 레벨을 3이상으로 올리고 나한테 오게 그럼 다시 말해 주겠네 \
그럼 행운을 비네 \ \
<닫  기/@exit>

[@Q703_2]
#IF
#SAY
그럼 다음 기회에 꼭 들어 주게.. 나중에 다시 들리게.. \ \
<닫  기/@exit>

[@normal]
#IF
#SAY
<$USERNAME>님은 미르 세계에 벌써 많은 것을 알고 있네. \
혼자서도 충분히 자기의 갈길을 개척해 나갈수 있다고 생각 되니 \
나는 자네에게 도움 줄 것이 없네.. 그럼 행운이 있기를 바라네. \ \
<닫  기/@exit>

[@buy]
무엇을 살지 고르시오.\ \ \
 <뒤로/@main>

[@sell]
팔 무기를 올려놓으시오.\ \ \
 <뒤로/@main>

[@repair]
무기를 고치시게? 어떤걸 고칠건지 올려보시오.\ \
 <뒤로/@main>

[~@repair]
잘 고쳐졌네, 다음에 또 들르게.\ \
 <뒤로/@main>

[@qweapon]
상점에서 파는 무기는 무기의 능력치가 일정하게 정해져있네.\
하지만 몬스터에게서 나오는 무기는 경우에따라 특별한 능력을\
더 가지고 있는 무기가 나오기도 하지. 그걸 상점으로 가져오면\
가격을 조금 더 쳐주니 잊지말게나\
참, 무기의 가격은 종류에따라 다르지만 대체적으로 내구도가\
높을수록 더 비싸다네.\ \
 <뒤로/@main>

[상품]
;젠되는 상품    물량       hour
목검            50         1
청동검          50         1
철검            50         1
유월도          50         1
단검            50         1
